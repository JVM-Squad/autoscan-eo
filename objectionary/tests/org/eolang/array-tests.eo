# The MIT License (MIT)
#
# Copyright (c) 2016-2022 Yegor Bugayenko
#
# Permission is hereby granted, free of charge, to any person obtaining a copy
# of this software and associated documentation files (the "Software"), to deal
# in the Software without restriction, including without limitation the rights
# to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
# copies of the Software, and to permit persons to whom the Software is
# furnished to do so, subject to the following conditions:
#
# The above copyright notice and this permission notice shall be included
# in all copies or substantial portions of the Software.
#
# THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
# IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
# FITNESS FOR A PARTICULAR PURPOSE AND NON-INFRINGEMENT. IN NO EVENT SHALL THE
# AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
# LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
# OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
# SOFTWARE.

+package org.eolang
+alias org.eolang.txt.sprintf
+alias org.eolang.io.stdout
+alias org.eolang.hamcrest.assert-that
+junit

[] > makes-array-through-special-syntax
  eq. > @
    2
    (* 1 2).length

[] > gets-lengths-of-empty-array-through-special-syntax
  eq. > @
    0
    *.length

[] > iterates-with-each
  *
    "one"
    "two"
    "three"
  .each > @
    [i] (stdout i > @)

[] > simple-mapping-int-to-string
  eq. > @
    length.
      map.
        * 1 2 3
        [i]
          i.as-string > @
    3

[] > using-map-as-object
  eq. > @
    " ".joined
      map.
        * 1 2
        [i]
          i.as-string > @
    "1 2"

# check that an empty array's .length equals to zero
[] > empty-array-length
  [elements...] > arr
  eq. > @
    0
    (arr).elements.length

# check that array.length works properly for non-empty arrays
[] > non-empty-array-length-test
  [elements...] > arr
  eq. > @
    5
    (arr "a" "b" "c" "d" "e").elements.length

# if map does mutate arrays, then this test would fail
# because of the absence of the as-int attribute
# that is caused by mutation of the original string array
# to an int array
[] > map-does-dot-mutate-array
  [numbers...] > squares
    map. > @
      numbers
      [current]
        pow. > @
          current.as-int
          2
  (squares "1" "2" "3") > sq
  eq. > @
    (sprintf "%d %d %d" 1 4 9)
    (sprintf "%d %d %d" (sq.get 0) (sq.get 1) (sq.get 2))

# this test performs mapping of array using the index for it
[] > mapi-index-can-be-accessed
  [numbers...] > multipliedByNaturalSequence
    mapi. > @
      numbers
      [current index]
        mul. > @
          current.as-int
          index.add 1
  (multipliedByNaturalSequence "1" "2" "3") > mulSeq
  eq. > @
    (sprintf "%d %d %d" 1 4 9)
    (sprintf "%d %d %d" (mulSeq.get 0) (mulSeq.get 1) (mulSeq.get 2))

[] > array-as-a-bound-attribute-size-0
  * > anArray
  0.eq (anArray.length) > @

[] > array-as-a-bound-attribute-size-1
  * > anArray
    100
  ((anArray.get 0).eq 100) > @

[] > array-as-a-bound-attribute-size-2
  * > anArray
    1
    2
  ((anArray.get 0).eq 1).and ((anArray.get 1).eq 2) > @

# check that array.is-empty works properly
[] > should-not-be-empty
  * > xs
    1
    2
  xs.is-empty.not > @

# check that array.is-empty works properly
[] > should-be-empty
  * > xs
  xs.is-empty > @

# arrays of objects
[] > should-not-be-empty-with-3-objects
  [x] > a-1
    (x.as-int > @)
  [x] > a-2
    (x.as-byte > @)
  [x] > a-3
    (x.as-string > @)
  * > xs
    a-1
    a-2
    a-3
  xs.is-empty.not > @

# arrays with anonymous object
[] > should-not-be-empty-with-1-anon-object
  * > xs
    [f]
      f.as-int > @
  xs.is-empty.not > @

# arrays with anonymous one-line-object
[] > should-not-be-empty-with-1-anon-object-one-liner
  * > xs
    [f] (f.as-int > @)
  xs.is-empty.not > @

# array equal to another array
[] > array-equal-to-another-array
  eq. > @
    *
      1
      "str"
      'f'
      22.0
      "друг".as-bytes
      TRUE
    *
      1
      "str"
      'f'
      (11.mul 2).as-float
      D0-B4-D1-80-D1-83-D0-B3
      FALSE.not

# array not equal to another object by length
[] > array-not-equal-to-another-array-by-length
  not. > @
    eq.
      * 1 2 3
      * 1 2 3 4

# check that varargs eq works
[] > varargs-eq-should-works
  [args...] > a
  [args...] > b
  eq. > @
    args.
      a
        13.as-float
        's'
        "bbt"
    args.
      b
        13.as-float
        's'
        "bbt"

# nested array equal object should works
[] > nested-array-eq-should-works
  * 1 "2" 3.0 > a
  * 1 "2" (3.as-float) > b
  eq. > @
    * 'w' a
    * 'w' b

# two different arrays not equal
[] > two-different-arrays-not-equal
  not. > @
    eq.
      * 1 2 3
      * 1 5 "text"

#two different nested arrays not eq
[] > nested-array-not-eq-should-works
  * 5 'o' "18" > a
  * 5 'm' (18.as-string) > b
  not. > @
    eq.
      * "not eq" a
      * "not eq" b

# check that varargs not eq works
[] > varargs-not-eq-should-works
  [args...] > a
  [args...] > b
  not. > @
    eq.
      args.
        a 1 2 3
      args.
        b 1 2 "fail"

# reduce with index test
[] > reduce-with-index-test
  * 2 2 > src
  eq. > @
    reducei.
      * 1 1
      0
      [a i x]
        add. > @
          x
          a.add
            src.get i
    6

# reduce without index test
[] > reduce-without-index-test
  eq. > @
    reduce.
      * 1 2 3 4
      -1
      [a x]
        mul. > @
          a
          x
    -24

[] > array-with-in-seq
  [a] > foo
    seq > @
      a.with "hi" > t
      t
  seq > @
    foo
      * 1 2 3
    TRUE

[] > array-with
  assert-that > @
    with.
      * 1 2 3
      "with"
    $.equal-to
      * 1 2 3 "with"

[] > array-without
  assert-that > @
    without.
      * 1 2 3
      1
    $.equal-to
      * 1 3

[] > array-without-complex-case
  [a] > foo
    seq > @
      a.without 0 > t
      t
  assert-that > @
    foo
      * 1 "text" 'f'
    $.equal-to
      * "text" 'f'

[] > array-without-nested-array
  * 3 2 1 > nested
  assert-that > @
    without.
      * "smthg" 27 nested
      2
    $.equal-to
      * "smthg" 27

[] > mapi-should-works
  assert-that > @
    mapi.
      * 1 2 3 4
      [x i]
        x.mul 2 > @
    $.equal-to
      * 2 4 6 8
