# The MIT License (MIT)
#
# Copyright (c) 2016-2022 Yegor Bugayenko
#
# Permission is hereby granted, free of charge, to any person obtaining a copy
# of this software and associated documentation files (the "Software"), to deal
# in the Software without restriction, including without limitation the rights
# to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
# copies of the Software, and to permit persons to whom the Software is
# furnished to do so, subject to the following conditions:
#
# The above copyright notice and this permission notice shall be included
# in all copies or substantial portions of the Software.
#
# THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
# IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
# FITNESS FOR A PARTICULAR PURPOSE AND NON-INFRINGEMENT. IN NO EVENT SHALL THE
# AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
# LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
# OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
# SOFTWARE.

+package org.eolang
+home https://github.com/objectionary/eo
+version 0.23.11
+rt jvm org.eolang:eo-runtime:0.23.11

[] > int

  [x] > xor
    as-int. > @
      xor.
        ^.as-bytes
        x.as-bytes

  [x] > or
    as-int. > @
      or.
        ^.as-bytes
        x.as-bytes

  [x] > and
    as-int. > @
      and.
        ^.as-bytes
        x.as-bytes

  # Calculate bitwise left shift
  [x] > left
    as-int. > @
      left.
        ^.as-bytes
        x

  # Calculate bitwise right shift
  [x] > right
    as-int. > @
      right.
        ^.as-bytes
        x

  # Tests that $ = x
  [x] > eq
    eq. > @
      ^.as-bytes
      x.as-bytes

  # Tests that $ â‰  x
  [x] > neq
    (^.eq x).not > @

  # Tests that $ < x
  [x] > lt
    and. > @
      (^.eq x).not
      (^.gt x).not

  # Tests that $ â‰¤ x
  [x] > lte
    or. > @
      ^.eq x
      ^.lt x

  # Tests that $ > x
  [x] > gt /bool

  # Tests that $ â‰¥ x
  [x] > gte
    or. > @
      ^.eq x
      ^.gt x

  # Change the sign of the number
  [] > neg
    ^.times -1 > @

  # Add to the current one
  [x] > plus /int

  # Subtract from the current one
  [x] > minus
    ^.plus (x.neg) > @

  # Multiplication this one by another int
  [x] > times /int

  # Divide this one by another int
  [x] > div /int

  # Calculate MOD
  [x] > mod
    ^ > dividend
    x > divisor

    if. > @
      divisor.eq 0
      []
        "mod by zero" > msg
      if.
        dividend.gt 0
        abs-mod
        neg.
          abs-mod

    [] > abs-mod
      dividend.abs > int-abs
      divisor.abs > x-abs
      minus. > @
        int-abs
        x-abs.times
          int-abs.div x-abs

  # Make it power x
  [x] > pow
    if. > @
      and.
        ^.eq 0
        x.lt 0
      []
        "0 cannot be raised to a negative power" > msg
      as-int.
        pow.
          ^.as-float
          x.as-float

  # Converts this to float
  [] > as-float /float

  # Converts this to string
  [] > as-string /string

  # Converts this to bytes
  [] > as-bytes /bytes

  # Converts this to hash
  [] > as-hash /int

  # Absolute value of $ (i.e., with no sign)
  [] > abs
    if. > @
      ^.gte 0
      ^
      ^.neg
